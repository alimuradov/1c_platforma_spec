
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ДолгиКоммитентам.Записывать = Истина;
	Для Каждого ТекСтрокаКоммитенты Из Коммитенты Цикл
		
		Движение = Движения.ДолгиКоммитентам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Коммитент = ТекСтрокаКоммитенты.Коммитент;
		Движение.Долг = ТекСтрокаКоммитенты.Сумма;
		
	КонецЦикла;

	ДВижения.ДолгиКоммитентам.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасходДенегКоммитенты.Коммитент КАК Коммитент
		|ПОМЕСТИТЬ ВТ_ДанныеДок
		|ИЗ
		|	Документ.РасходДенег.Коммитенты КАК РасходДенегКоммитенты
		|ГДЕ
		|	РасходДенегКоммитенты.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДолгиКоммитентамОстатки.Коммитент КАК Коммитент,
		|	ДолгиКоммитентамОстатки.Коммитент.Представление КАК КоммитентПредставление,
		|	-ДолгиКоммитентамОстатки.ДолгОстаток КАК ДолгОстаток
		|ИЗ
		|	РегистрНакопления.ДолгиКоммитентам.Остатки(
		|			&Граница,
		|			Коммитент В
		|				(ВЫБРАТЬ
		|					ВТ_ДанныеДок.Коммитент КАК Коммитент
		|				ИЗ
		|					ВТ_ДанныеДок КАК ВТ_ДанныеДок)) КАК ДолгиКоммитентамОстатки
		|ГДЕ
		|	ДолгиКоммитентамОстатки.ДолгОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Превышена сумма выплаты коммитенту %1 на %2", ВыборкаДетальныеЗаписи.КоммитентПредставление, ВыборкаДетальныеЗаписи.ДолгОстаток);
		Сообщение.Сообщить();
		
	КонецЦикла;
	
КонецПроцедуры
