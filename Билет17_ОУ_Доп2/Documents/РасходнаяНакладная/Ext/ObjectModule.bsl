
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	Движения.ДолгиКоммитентам.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ДанныеДок
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	|	ОстаткиНоменклатурыОстатки.Коммитент КАК Коммитент,
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиНом
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТ_ДанныеДок.Номенклатура КАК Номенклатура
	|				ИЗ
	|					ВТ_ДанныеДок КАК ВТ_ДанныеДок)) КАК ОстаткиНоменклатурыОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Коммитент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДок.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДок.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВТ_ДанныеДок.Количество КАК Количество,
	|	ВТ_ДанныеДок.Сумма КАК Сумма,
	|	ВТ_ОстаткиНом.Партия КАК Партия,
	|	ВТ_ОстаткиНом.Коммитент КАК Коммитент,
	|	ЕСТЬNULL(ВТ_ОстаткиНом.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ПроцентыКомминентамСрезПоследних.Процент, 0) КАК ПроцентКоммитента
	|ИЗ
	|	ВТ_ДанныеДок КАК ВТ_ДанныеДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиНом КАК ВТ_ОстаткиНом
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыКомминентам.СрезПоследних(
	|					&МоментВремени,
	|					Коммитент В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ВТ_ОстаткиНом.Коммитент КАК Коммитент
	|						ИЗ
	|							ВТ_ОстаткиНом КАК ВТ_ОстаткиНом)) КАК ПроцентыКомминентамСрезПоследних
	|			ПО ВТ_ОстаткиНом.Коммитент = ПроцентыКомминентамСрезПоследних.Коммитент
	|		ПО ВТ_ДанныеДок.Номенклатура = ВТ_ОстаткиНом.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ОстаткиНом.Партия.МоментВремени
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(Сумма),
	|	МАКСИМУМ(ПроцентКоммитента),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура,
	|	Коммитент";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		НеХватает = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если НеХватает > 0 Тогда
		
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает товара %1 в количестве %2", ВыборкаНоменклатура.НоменклатураПредставление, НеХватает);
			Сообщение.Сообщить();
			
		КонецЕсли;
		
		
		Если Отказ Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		ВыборкаКоммитент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ИтогоСписать = ВыборкаНоменклатура.Количество;
		Пока ИтогоСписать > 0 И ВыборкаКоммитент.Следующий() Цикл
			
			ВыборкаДетальныеЗаписи = ВыборкаКоммитент.Выбрать();
			
			Списано = 0;
			Пока ИтогоСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				СписатьПоПартии = МИН(ИтогоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				ИтогоСписать = ИтогоСписать - СписатьПоПартии;
				
				Движение = Движения.ОстаткиНоменклатуры.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
				Движение.Коммитент = ВыборкаДетальныеЗаписи.Коммитент;
				Движение.Количество = СписатьПоПартии;
				
				Списано = Списано + Движение.Количество;
				
			КонецЦикла;
			
			ДоляПродажКоммитента = Списано / ВыборкаКоммитент.Количество * ВыборкаКоммитент.Сумма;
			СуммаКоммитенту = ВыборкаКоммитент.ПроцентКоммитента * ДоляПродажКоммитента / 100; 
			
			Если СуммаКоммитенту > 0 Тогда
				Движение = Движения.ДолгиКоммитентам.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Коммитент = ВыборкаКоммитент.Коммитент;
				Движение.Долг = СуммаКоммитенту;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры
